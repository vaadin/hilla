import org.jetbrains.kotlin.gradle.dsl.JvmTarget

/***********************************************************************************************************************
 *
 * Plugins
 *
 **********************************************************************************************************************/
plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'idea'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.dokka' version '1.9.0'
}

/***********************************************************************************************************************
 *
 * Basic properties
 *
 **********************************************************************************************************************/

def pom = new XmlSlurper().parse(rootProject.file('pom.xml'))
defaultTasks('jar', 'test')
group pom.parent.groupId
version = project.hasProperty('hilla.version') ? project.getProperty('hilla.version') : pom.parent.version
archivesBaseName = pom.artifactId
sourceCompatibility = 17
targetCompatibility = 17
def rootPom = new XmlSlurper().parse(rootProject.file('../../../pom.xml'))
def springBootVersion = rootPom.properties['spring.boot.version']
def flowVersion = rootPom.properties['flow.version']

/***********************************************************************************************************************
 *
 * Sources
 *
 **********************************************************************************************************************/

sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

/***********************************************************************************************************************
 *
 * Repositories & Dependencies
 *
 **********************************************************************************************************************/

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://plugins.gradle.org/m2/' }
    maven { url = 'https://repo.spring.io/snapshot' }
    maven { url = 'https://repo.spring.io/milestone' }
    maven { url = 'https://maven.vaadin.com/vaadin-prereleases' }
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
    implementation("com.vaadin:hilla-engine-core:$version")
    implementation("com.vaadin:flow-gradle-plugin:$flowVersion")
    implementation("com.vaadin:flow-plugin-base:$flowVersion")
    implementation("org.springframework.boot:spring-boot-loader-tools:$springBootVersion")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.jetbrains.kotlin:kotlin-test:$kotlinVersion")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion")
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

/***********************************************************************************************************************
 *
 * Artifacts
 *
 **********************************************************************************************************************/

jar {
    manifest{
        attributes(
                'Hilla-Package-Version': 1,
                'Implementation-Title': 'Hilla Gradle Plugin',
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor': 'Vaadin Ltd',
        )
    }
    from 'gradle.properties'
}

/**
 * Plugin artifact sources.
 */
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    include 'com/**/*.groovy'
    include 'com/**/*.java'
    include 'com/**/*.kt'
}

/**
 * Artifacts in build
 */
artifacts {
    archives jar
    archives sourcesJar
}

/***********************************************************************************************************************
 *
 * Testing & Quality
 *
 ***********************************************************************************************************************/
task functionalTest(type: Test) {
    dependsOn test, jar
    group = 'Verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    maxParallelForks = 1
    jvmArgs('-Xms512M', '-Xmx1024M')
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
check.dependsOn functionalTest
/***********************************************************************************************************************
 *
 * Deployment
 *
 ***********************************************************************************************************************/
gradlePlugin {
    testSourceSets sourceSets.functionalTest
    plugins {
        hillaPlugin {
            id = 'com.vaadin.hilla'
            implementationClass = 'com.vaadin.hilla.gradle.plugin.HillaPlugin'
        }
    }
}

pluginBundle {
    website = 'https://hilla.dev/docs/react/start/gradle'
    vcsUrl = 'https://github.com/vaadin/hilla/tree/main/packages/java/gradle-plugin'
    description = 'Build Hilla applications with Gradle.'
    tags = ['hilla']
    plugins {
        hillaPlugin {
            id = gradlePlugin.plugins.hillaPlugin.id
            displayName = 'Hilla Gradle Plugin'
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.archivesBaseName
    }
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version

			from components.java
		}
	}
}

/***********************************************************************************************************************
 *
 * Misc.
 *
 **********************************************************************************************************************/
wrapper {
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.ALL
}

// This will print ongoing tests, taken from https://stackoverflow.com/a/4292739/377320
// This will come in handy on the CI during long-running tests to prevent the CI killing
//     the process due to a false indication of inactivity:
functionalTest {
    testLogging {
        info.events "passed", "skipped", "failed"
    }
}

kotlin {
    explicitApi()
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
    explicitApi()
}
